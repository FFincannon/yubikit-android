apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion project.compileSdkVersion
    defaultConfig {
        applicationId "com.yubico.yubikit.demo"
        minSdkVersion project.minFidoSdkVersion
        targetSdkVersion project.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "APP_NAME", "\"YubikitDemo\""
        buildConfigField "String", "NAMESPACE", "\"webauthnflow\""
        buildConfigField "String", "SERVER_URL", "\"https://demo.yubico.com\""
        buildConfigField "String", "TABLE_NAME", "\"authenticators\""
    }

    signingConfigs {
        // Use the provided 'debug.keystore' for signing so that the webauthn
        // server accepts the package signature
        release {
            storeFile file('../keystore/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.release
        }
    }
}

configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    implementation project(":fidodemo")

    // yubikit
    implementation project(':yubikit')
    implementation (project(':oath')) {
        exclude module:'yubikit'
    }
    implementation (project(':otp')) {
        exclude module:'yubikit'
    }
    implementation (project(':management')) {
        exclude module:'yubikit'
    }

    // ui libraries
    def appcompatVersion = "1.0.2"
    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    def androidxuiVersion = "1.0.0"
    implementation "androidx.preference:preference:$androidxuiVersion"

    // webview
    implementation "androidx.browser:browser:$androidxuiVersion"

    // architecture/lifecycle
    def lifecycleVersion = "1.1.0"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycleVersion"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.0.0'

    // navigation
    def nav_version = "2.1.0-alpha05"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version" // For Kotlin use navigation-fragment-ktx
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version" // For Kotlin use navigation-ui-ktx

    // network
    def retrofitVersion = "2.4.0"
    def okhttpVersion = "3.11.0"
    def moshiVersion = "1.8.0"
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    implementation "com.squareup.moshi:moshi-kotlin:$moshiVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"

    // decoders
    implementation 'commons-codec:commons-codec:1.11'

    // unit tests
    def mockito_version = "2.11.0"
    def robolectric_version = "4.2"
    def junit_version = "1.1.1"
    testImplementation 'junit:junit:4.12'
    testImplementation "androidx.test.ext:junit:$junit_version"
    testImplementation "org.robolectric:robolectric:$robolectric_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"

    // ui tests
    // Core library
    def ui_test_version = "1.2.0"
    androidTestImplementation "androidx.test:core:$ui_test_version"

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation "androidx.test:runner:$ui_test_version"
    androidTestImplementation "androidx.test:rules:$ui_test_version"

    // Assertions
    androidTestImplementation "androidx.test.ext:junit:$junit_version"
    androidTestImplementation "androidx.test.ext:truth:$ui_test_version"
    androidTestImplementation 'com.google.truth:truth:0.42'

    // Espresso dependencies
    def espresso_version = "3.2.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso_version"
}
